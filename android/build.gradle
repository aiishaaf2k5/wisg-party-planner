// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:8.7.3'
        classpath 'com.google.gms:google-services:4.4.4'
    }
}

apply from: "variables.gradle"

allprojects {
    repositories {
        google()
        mavenCentral()
    }

    configurations.configureEach {
        // Prevent duplicate Kotlin classes from mixed stdlib variants.
        exclude group: "org.jetbrains.kotlin", module: "kotlin-stdlib-jdk7"
        exclude group: "org.jetbrains.kotlin", module: "kotlin-stdlib-jdk8"

        resolutionStrategy.eachDependency { details ->
            if (details.requested.group == "org.jetbrains.kotlin") {
                details.useVersion "1.8.22"
            }
        }
    }
}

subprojects { sub ->
    if (sub.name == "capacitor-cordova-android-plugins" || sub.name == "capacitor-android") {
        sub.tasks.configureEach { t ->
            def n = t.name.toLowerCase()
            if (n.contains("androidtest") || n.contains("unittest")) {
                t.enabled = false
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
